:Class BrazilNut :MiPage

    :Field public script    ⍝ Editable simulation APLPhys script
    :Field public maxatoms  ⍝ Maximum allowed number of particles for demo
    :Field public natoms    ⍝ Number of particles / balls
    :Field public dumpfreq  ⍝ Number of steps before visual position update
    :Field public dt        ⍝ Timestep (reduced LJ units)
    :Field public pos       ⍝ Ball positions
    :Field public play      ⍝ Simulation play state
    :Field public step      ⍝ Simulation step number
    :Field public bs        ⍝ Ball size
    :Field public bw        ⍝ Border width 
    :Field public groups    ⍝ Atom groups 

    ∇ Compose
      :Access public
    ⍝ --- POPUPS AND STYLING ---
      Use'jBox'
      Use'dcPanel'
      Add _.StyleSheet'Assets/LJmelt.css'
    ⍝ --- CONTROL ---
      ctrlpanel←'.ctrlpanel'Add _.div
      ctrlpanel.Add _.h4'Brazil Nut Effect'
      ctrlpanel.Add _.p'For now, just get 3D working'
      ctrlform←'.ctrlform'ctrlpanel.Add _.form
      '#runscript'ctrlform.Add _.Button'Run script'
      (Add _.Handler'#runscript' 'click' 'RunScript').Hourglass←0
      '#startstop' 'onclick="startStop();"'ctrlform.Add _.Button'Start/Stop'
      '#steponce' 'onclick="StepOnce();"'ctrlform.Add _.Button'Step once'
      '#step'ctrlform.Add _.p'Step: '
      Add _.br
    ⍝ --- SIMULATION INPUT AND RENDERING OPTIONS ---
      ctrltabs←'ctrltabs'ctrlpanel.Add _.Tabs
    ⍝ --- INPUT SCRIPT ---
      ctrltabs.Titles,←⊂'Input Script'
      inscript←ctrltabs.Sections,←'inscript'New _.div
      inform←inscript.Add _.form
      script←⊂'⍝ BrazilNut.aplphys'
      script,←⊂'⍝ Demo APLPhys script'
      script,←⊂'⍝ Simulation parameters'
      script,←⊂'periodic ← 1'
      script,←⊂'∆t ← 0.0032'
      script,←⊂'fixtemp ← 0.5'
      script,←⊂' '
      script,←⊂'CreateBox 30 30 30'
      script,←⊂'CreateAtoms 32 random random' 
      script,←⊂'groups,←natoms'
      script,←⊂'fixgroups,←0'
      script,←⊂'PairStyle ''LJcutOpt'' 2.5 1'
      script,←⊂'Thermostat ← TempRescale'
      script,←⊂'RunStyle ← Verlet'
      script,←⊂' '
      script,←⊂'dumpfreq ← 10'
      script←script,¨⎕UCS 10
      inform.Add _.br
      '#aplphys name=aplphys'inform.Add _.textarea script
    ⍝ --- BABYLON RENDERER SETTINGS ---
    ⍝     Modify simulation display e.g. atom colours
      ctrltabs.Titles,←⊂'Render Settings'
      babyset←ctrltabs.Sections,←'babySet'New _.div
      babyset.Add _.p'Babylon Renderer Settings'  
    ⍝ --- VIEWPORT ---
      viewport←'viewport'Add _.canvas
      ⍝viewport.Add _.jqDraggable'#viewport'
      Add _.Timer 1600 0 'tick' 'T' ⍝ Tick with update
      (On'tick' 'Tick').Hourglass←0
      play←0              
    ⍝ --- Add HTML5 canvas rendering scripts ---
      Add _.Script'' '/3drender.js'
      Add _.Script'' '/babylon.js' ⍝ Local copy
      ⍝Add _.Script'' 'https://preview.babylonjs.com/babylon.js'
    ∇

    ∇ js←Init
      js←⍬
    ⍝ Link MiPhys with APLPhys parameters
      :Trap 0
          #.APLPhys.(acc←⊃ComputeForces pos)
      :Else
          content←New _.div
          content.Add _.p'Error running APLPhys script'
          content.Add _.pre(∊⎕DM,¨⎕UCS 10)
          js,←Execute _.jBox.Modal content
      :EndTrap
      dumpfreq←#.APLPhys.dumpfreq
      natoms←#.APLPhys.natoms
      labels←'p',↑⍕¨⍳natoms
      boxdim←#.APLPhys.boxdim
      ∆t←#.APLPhys.∆t
      pos←#.APLPhys.pos
      ballsize←#.APLPhys.rcutoff
    ∇

    ∇ js←RunScript;content
      :Access public
      js←⍬
      script←''Get'aplphys' ⍝'inscript'
      script←(⎕UCS 10 13){⍵⊆⍨∧⌿⍺∘.≠⍵}script
      #.APLPhys.Init
      #.APLPhys.⎕FX'aplscript' '',script
      :Trap 0
          #.APLPhys.aplscript
      :Else
          content←New _.div
          content.Add _.p'Error running APLPhys script'
          content.Add _.pre(∊⎕DM,¨⎕UCS 10)
          js,←Execute _.jBox.Modal content
      :EndTrap
      step←0
      js,←Init
      js,←Execute'wakeBaby();' ⍝ Initialise rendering engine
      js,←Execute'posQueue=[];'⍝ Clear animation frames
      js,←Execute'groups=',(⎕JSON #.APLPhys.groups),';'  ⍝ Number of atoms in each group 
      js,←Execute'babyMenu();' ⍝ Set up the Babylon Render Settings menu
      js,←Execute'dumpfreq=',dumpfreq,';'
      pre_time←0 ⍝ Previous request time
      nframes←5 ⍝ Initial guess at number of frames to request
      pnframes←5 ⍝ Previous number of frames requested
    ∇

    ∇ js←Tick;i;poslist
      js←⍬
      ⍝⎕←'---Tick---'
      re_time←Get'reTime'
      :Access public
      poslist←⍬
      ⍝ Increase number of frames to process for each request
      ⍝   unless the request time increases
      :Trap 6
          ∆ren←re_time-pre_time ⍝ ∆request_time
          nframes←1⌈nframes-×∆ren        
          :Trap 0                        
              :For i :In ⍳nframes           
                  #.APLPhys.(pos vel acc←3↑ComputeForces RunStyle⍣dumpfreq⊢pos vel acc ⍬ ⍬ ⍬ ⍬)
                  r←{e÷⍨⌊0.5+#.APLPhys.pos×e←10*⍵}3 ⍝ Reduced LJ units | Rounded to 3dp
                  step+←dumpfreq
                  poslist,←⊂r
              :EndFor
          :Else
              content←New _.div
              content.Add _.p'Error running APLPhys script'
              content.Add _.pre(∊⎕DM,¨⎕UCS 10)
              js,←Execute'if (playing){startStop();}'
              js,←Execute _.jBox.Modal content
          :EndTrap
          pre_time←re_time
          js,←Execute'posQueue.push(',(¯1↓1↓⎕JSON↓¨poslist),')'
      :Else
          js←Execute'if (playing){startStop();}'
          js,←Execute _.jBox.Modal'Run the script before starting the simulation'
      :EndTrap
    ∇

:EndClass
