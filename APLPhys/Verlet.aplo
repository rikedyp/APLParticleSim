 Verlet←{
   ⍝ Velocity-Verlet numerical integrator
     (pos vel acc _ _ _ _)←⍵ ⍝ Previous state
     pos+←∆t×vel+0.5×acc×∆t ⍝ Step position
     pos←periodic|pos ⍝ Fold positions if periodic boundary conditions set
     vel+←0.5×∆t×acc ⍝ Half-step velocity
     (ene_kin_avg temp)←ComputeTemp vel
     vel←temp Thermostat vel ⍝ Thermostat rescale velocities
     (acc ene_pot_avg virial)←⍺⍺ pos vel⍝ Compute forces
     acc+⍤1←allforce ⍝ Add uniform force e.g. gravity
     acc[⍸groups/fixgroups;]←0 ⍝ Fixed atoms don't experience forces
     vel+←0.5×∆t×acc ⍝ Complete velocity step
     (ene_kin_avg temp)←ComputeTemp vel
     pressure←(density×temp)+virial÷volume
     pos vel acc ene_kin_avg ene_pot_avg temp pressure
 }
