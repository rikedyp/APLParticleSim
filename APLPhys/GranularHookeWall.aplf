 GranularHookeWall←{
⍝ ⍵: pos vel
     (pos vel)←⍵
⍝ ⍺: kn gn mass radii walls
     (kn gn mass radii walls wvel)←⍺
⍝ ⍵: pos vel
⍝ ←: acc
⍝ Helper functions
     MergeLayers←,[⍳2]
     Mag←{0.5*⍨+/⍵*2} ⍝ Magnitude of vector in Euclidean space
     Norm←÷∘Mag⍨ ⍝ Normalised vector
     InDirOf←(⊢×+.×)∘Norm ⍝ Component of vector ⍺ in direction of ⍵
⍝ Plane normals
     norms←(Norm 3↑⊢)⍤1⊢walls
⍝ Overlap distance from points to planes
     ∆S←radii-⍤1⊢walls DistPlane2Point⍤1⍤1 99⊢pos
⍝ Overlap distance of particles
     touchmask←∆S>0
⍝     touchmask∧←∘.>⍨⍳≢touchmask
     ∆Scalc←(,touchmask)⌿MergeLayers ∆S
     calcpos←⍸,touchmask
⍝     0
⍝ Relative velocities of particles and walls
     vr←-vel-⍤1⍤1 99⍨wvel
⍝ Normal component of relative velocity of 2 particles vn
     vn←+⌿vr InDirOf⍤1⍤2 1⊢norms
     ⍝(,touchmask)⌿MergeLayers vn
⍝ Effective mass meff
     meff←mass ⍝ Wall mass is infinite
⍝ force = (kn×∆S×norms)-(meff×gn×vn)
     force←(+⌿kn×norms×⍤1 0⍤1⊢∆S)-(gn×meff×⍤0 1⊢vn)
     force÷⍤1 0⊢meff
 }
